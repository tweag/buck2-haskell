# Copyright (c) Meta Platforms, Inc. and affiliates.
#
# This source code is licensed under both the MIT license found in the
# LICENSE-MIT file in the root directory of this source tree and the Apache
# License, Version 2.0 found in the LICENSE-APACHE file in the root directory
# of this source tree.

load(
    "@prelude//linking:link_info.bzl",
    "LinkStyle",
)
load("@prelude//utils:utils.bzl", "dedupe_by_value", "flatten")
load(
    ":toolchain.bzl",
    "HaskellToolchainLibrary",
)

HaskellPackageConfInfo = record(
    final_conf = Artifact | None,
    empty_conf = Artifact | None,
    deps_conf = Artifact | None,
)

# A record of a Haskell library.
HaskellLibraryInfo = record(
    # The library target name: e.g. "rts"
    name = str,
    # package config database: e.g. platform009/build/ghc/lib/package.conf.d
    db = Artifact,
    # package config database, referring to the empty lib which is only used for compilation
    empty_db = Artifact | None,
    # package config database, used for ghc -M
    deps_db = Artifact | None,
    # e.g. "base-4.13.0.0"
    id = str,
    # conf files
    conf = HaskellPackageConfInfo,
    # dynamic dependency information
    dynamic = None | dict[bool, DynamicValue],
    # Interface files indexed by profiling enabled/disabled
    interfaces = dict[bool, list[Artifact]],
    # Object files indexed by profiling enabled/disabled
    objects = dict[bool, list[Artifact]],
    # HIE files indexed by profiling enabled/disabled
    hie_files = dict[bool, list[Artifact]],
    stub_dirs = list[Artifact],
    # extra non-Haskell libraries
    extra_libraries = field(list[Dependency], []),

    # resultant libraries
    libs = field(list[Artifact], []),
    # Package version, used to specify the full package when exposing it,
    # e.g. filepath-1.4.2.1, deepseq-1.4.4.0.
    # Internal packages default to 1.0.0, e.g. `fbcode-dsi-logger-hs-types-1.0.0`.
    version = str,
    is_prebuilt = bool,
    profiling_enabled = bool,

    # All dependencies (untyped) = in-project deps + toolchain deps
    # TODO: Make this typed by separating out in-project deps.
    dependencies = list[str],
    # Toolchain package dependencies
    toolchain_dependencies = list[HaskellToolchainLibrary],
    md_file = Artifact | None,
)

# If the target is a haskell library, the HaskellLibraryProvider
# contains its HaskellLibraryInfo. (in contrast to a HaskellLinkInfo,
# which contains the HaskellLibraryInfo for all the transitive
# dependencies). Direct dependencies are treated differently from
# indirect dependencies for the purposes of module visibility.
HaskellLibraryProvider = provider(
    fields = {
        "lib": provider_field(dict[LinkStyle, HaskellLibraryInfo] | None, default = None),
        "prof_lib": provider_field(dict[LinkStyle, HaskellLibraryInfo] | None, default = None),
    },
)

def _project_as_package_db(lib: HaskellLibraryInfo) -> cmd_args:
    return cmd_args(lib.db)

def _project_as_empty_package_db(lib: HaskellLibraryInfo) -> cmd_args:
    return cmd_args(lib.empty_db) if lib.empty_db != None else cmd_args()

def _project_as_deps_package_db(lib: HaskellLibraryInfo) -> cmd_args:
    return cmd_args(lib.deps_db) if lib.deps_db != None else cmd_args()

def _project_as_libs(lib: HaskellLibraryInfo) -> cmd_args:
    return cmd_args(lib.libs)

def _project_as_interfaces(lib: HaskellLibraryInfo) -> cmd_args:
    args = cmd_args()
    for _profiling, ifaces in lib.interfaces.items():
        args.add(ifaces)
    return args

def _get_package_deps(children: list[list[str]], lib: HaskellLibraryInfo | None) -> list[str]:
    flatted = flatten(children)
    if lib:
        flatted.extend(lib.dependencies)
    return dedupe_by_value(flatted)

def _get_toolchain_packages(
        children: list[list[HaskellToolchainLibrary]],
        lib: HaskellLibraryInfo | None) -> list[HaskellToolchainLibrary]:
    flatted = flatten(children)
    if lib:
        flatted.extend(lib.toolchain_dependencies)
    return dedupe_by_value(flatted)

# Used by the persistent worker in the build plan action to restore the target unit's transitive dependencies from cache
# into the unit env and module graph.
def _json_as_dep_units(lib: HaskellLibraryInfo) -> struct:
    return struct(
        name = lib.name,
        build_plan = lib.md_file,
    )

HaskellLibraryInfoTSet = transitive_set(
    args_projections = {
        "package_db": _project_as_package_db,
        "empty_package_db": _project_as_empty_package_db,
        "deps_package_db": _project_as_deps_package_db,
        "libs": _project_as_libs,
        "interfaces": _project_as_interfaces,
    },
    reductions = {
        "packages": _get_package_deps,
        "toolchain_packages": _get_toolchain_packages,
    },
    json_projections = {
        "dep_units": _json_as_dep_units,
    },
)

# Transitive set carrying (module_path, source_artifact) pairs from haskell_library targets.
# Each node's value is a list of (str, Artifact) where str is the path relative to the
# source root (strip_prefix applied), e.g. "App/Foo.hs".
HaskellSourcesTSet = transitive_set()

HaskellSourceInfo = provider(
    fields = {
        "srcs": provider_field(HaskellSourcesTSet),
    },
)
